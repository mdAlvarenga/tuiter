plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.5'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.challenge'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	annotationProcessor 'org.projectlombok:lombok'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'com.h2database:h2'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.testcontainers:junit-jupiter:1.20.6'
	testImplementation('org.testcontainers:postgresql:1.20.6')
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	constraints {
		implementation('org.apache.commons:commons-compress:1.27.1') {
			because 'Fixes CVE-2024-26308 y CVE-2024-25710 en testcontainers'
		}
	}
}

static def configurarTareaTest(Test task) {
	task.testLogging {
		events "passed", "skipped", "failed"
	}
}

tasks.register('tests', Test) {
	group = 'verification'
	description = 'Ejecuta todos los tests'
	useJUnitPlatform()
	configurarTareaTest(it)
}

tasks.register('testsUnitarios', Test) {
	group = 'verification'
	description = 'Ejecuta solo los tests unitarios (excluye @Tag("integration"))'
	useJUnitPlatform {
		excludeTags 'integration'
	}
	configurarTareaTest(it)
}

tasks.register('testsIntegracion', Test) {
	group = 'verification'
	description = 'Ejecuta solo los tests de integraci√≥n (@Tag("integration"))'
	useJUnitPlatform {
		includeTags 'integration'
	}
	configurarTareaTest(it)
}

tasks.named('test', Test) {
	group = 'verification'
	description = 'Tarea por defecto: ejecuta todos los tests'
	useJUnitPlatform()
	configurarTareaTest(it)
}
